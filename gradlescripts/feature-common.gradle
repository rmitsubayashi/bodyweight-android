apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: "$rootDir/gradlescripts/dependencies/general-dependencies.gradle"
apply from: "$rootDir/gradlescripts/dependencies/test-dependencies.gradle"
apply from: "$rootDir/gradlescripts/dependencies/dependencies.gradle"

apply from: "$rootDir/gradlescripts/custom-resource-directory-generator.gradle"

apply from: "$rootDir/gradlescripts/android-version-configs.gradle"

android {
    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        // we use Robolectric for unit testing and Espresso for instrumentation testing
        // the UI tests in androidTest folder
        test.java.srcDirs = ['src/unitTest/kotlin', 'src/androidTest/kotlin']
        androidTest.java.srcDirs = ['src/androidTest/kotlin']

        main.res.srcDirs = resourceDirectories
    }

    testOptions.unitTests {
        includeAndroidResources = true
        all {
            // normally look at the console to debug, so the html and xml files
            // are unnecessary.
            // TODO remove the directories generated by unit tests
            // (the report generation is suppressed, but the file generation isn't ;( )
            reports.html.enabled = false
            reports.junitXml.enabled = false
            // TODO remove the binary output instead of moving it to a file
            binResultsDir = file("$buildDir/outputs/unit-test")
        }
    }
}

dependencies {
    implementation generalDeps.values()
    implementation diDeps.values()
    testImplementation unitTestDeps.values()
    testImplementation robolectricDeps.values()
    testImplementation androidTestDeps.values()
    testImplementation espressoDeps.values()
    testImplementation assertionHelperDeps.values()
    testImplementation koinTestDeps.values()
    testImplementation fragmentTestDeps.values()
    
    androidTestImplementation unitTestDeps.values()
    androidTestImplementation androidTestDeps.values()
    androidTestImplementation espressoDeps.values()
    androidTestImplementation assertionHelperDeps.values()
    androidTestImplementation koinTestDeps.values()
    androidTestImplementation fragmentTestDeps.values()

}