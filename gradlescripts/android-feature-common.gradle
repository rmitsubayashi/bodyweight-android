apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: "$rootDir/gradlescripts/dependencies/general-dependencies.gradle"
apply from: "$rootDir/gradlescripts/dependencies/test-dependencies.gradle"
apply from: "$rootDir/gradlescripts/dependencies/debug-dependencies.gradle"
apply from: "$rootDir/gradlescripts/dependencies/dependencies.gradle"

apply from: "$projectDir/gradlescripts/resource-paths.gradle"

// needed by all the android feature gradle files
android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        targetCompatibility = "8"
        sourceCompatibility = "8"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'

        main.res.srcDirs = resourcePaths
    }

    testOptions.unitTests.all {
        // normally look at the console to debug, so the html and xml files
        // are unnecessary.
        // TODO remove the directories generated by unit tests
        // (the report generation is suppressed, but the file generation isn't ;( )
        reports.html.enabled = false
        reports.junitXml.enabled = false
        // TODO remove the binary output instead of moving it to a file
        binResultsDir = file("$buildDir/outputs/unit-test")
    }
}

dependencies {
    // TODO do we need this file tree??
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation generalDeps.values()
    debugImplementation memoryDeps.values()
    debugImplementation debuggerDeps.values()
    testImplementation unitTestDeps.values()
    androidTestImplementation androidTestDeps.values()
}