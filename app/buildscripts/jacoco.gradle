// https://www.veskoiliev.com/how-to-setup-jacoco-for-android-project-with-java-kotlin-and-multiple-flavours/
apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.4"
}

// needed for Robolectric
tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

tasks.create(name: "reportUnitTestCoverage", type: JacocoReport,
        dependsOn: ["testDebugUnitTest"]) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports."

    reports {
        html {
            enabled true
            destination file("$buildDir/jacoco/unit-test-reports/coverage")
        }
    }

    def excludes = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*com.github.rmitsubayashi.bodyweight.Test*.*',
            'android/**/*.*'
            //might also need to exclude Kotlin-generated code
            // https://medium.com/@andrey.fomenkov/kotlin-jacoco-tuning-compiler-to-skip-generated-code-935fcaeaa391
    ]
    def compiledKotlinClasses = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: excludes)
    classDirectories = files([compiledKotlinClasses])

    sourceDirectories = files([
            "$project.projectDir/src/main/kotlin"
    ])

    executionData = files("${project.buildDir}/jacoco/testDebugUnitTest.exec")

    // don't like how the testDebugUnitTest task creates 'jacoco', 'reports', 'test-results'
    // folders in the same file hierarchy.
    // so, we are moving all of them under the 'jacoco' folder and renaming them accordingly.
    doLast {
        ant.move(toDir: "$project.buildDir/jacoco/unit-test-reports/summary"){
            fileset(dir: "$project.buildDir/reports/tests/testDebugUnitTest")
        }
        delete "$project.buildDir/reports"
    }
    doLast {
        ant.move(toDir: "$project.buildDir/jacoco/unit-test-reports/individualResults"){
            fileset(dir: "$project.buildDir/test-results/testDebugUnitTest")
        }
        delete "$project.buildDir/test-results"
    }
}
