apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply from: 'dependencies/general-dependencies.gradle'
apply from: 'dependencies/test-dependencies.gradle'
apply from: 'dependencies/debug-dependencies.gradle'
apply from: 'dependencies/dependencies.gradle'

apply from: 'buildscripts/resource-paths.gradle'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.github.rmitsubayashi.bodyweight"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        targetCompatibility = "8"
        sourceCompatibility = "8"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'

        main.res.srcDirs = resourcePaths
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'retrofit.pro'
        }
    }

    testOptions.unitTests.all {
        // normally look at the console to debug, so the html and xml files
        // are unnecessary.
        // TODO remove the directories generated by unit tests
        // (the report generation is suppressed, but the file generation isn't ;( )
        reports.html.enabled = false
        reports.junitXml.enabled = false
        // TODO remove the binary output instead of moving it to a file
        binResultsDir = file("$buildDir/outputs/unit-test")
    }


    dynamicFeatures = [":shop"]


}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation generalDeps.values()
    implementation networkDeps.values()
    implementation rxDeps.values()
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    debugImplementation memoryDeps.values()
    debugImplementation debuggerDeps.values()
    testImplementation unitTestDeps.values()
    androidTestImplementation androidTestDeps.values()
}
